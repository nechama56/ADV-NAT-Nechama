* >Natural Source Header 000000
* :Mode S
* :CP windows-1255
* :LineIncrement 10
* <Natural Source Header
* THIS PROGRAM DEMONSTRATES HOW EXPENSIVE IT IS TO EXAMINE AN ARRAY
* AS OPPOSED TO EXAMINE'ING A STRING.
*
DEFINE DATA LOCAL
1 #STRING-TABLE (200)
  2 #ARRAY (A4)
  2 #DUMMY (A1)
1 REDEFINE #STRING-TABLE
  2 #STRING (a1000)
1 #LOOP (P9)
1 #NUMBER (P3)
1 #CPU-START (P9)
1 #CPU-ELAPSED (P9)
END-DEFINE
*
* INCLUDE AATITLER
* INCLUDE AASETC
* *
MOVE 'XXX' TO #ARRAY (10)
MOVE *CPU-TIME TO #CPU-START
SETTIME
FOR #LOOP = 1 TO 300000
IGNORE
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 3/10 'CONTROL LOOP TIME==>' *TIMD (0026) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETTIME
FOR #LOOP = 1 TO 300000
EXAMINE #STRING FOR 'XXX' GIVING NUMBER #NUMBER
*
IF #NUMBER = 0
  IGNORE
  END-IF
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 3/10 'STRING TIME==>' *TIMD (0034) #CPU-ELAPSED
*
MOVE *CPU-TIME TO #CPU-START
SETTIME
FOR #LOOP = 1 TO 300000
EXAMINE #ARRAY (*) FOR 'XXX' GIVING INDEX #NUMBER
*
IF #NUMBER = 0
  IGNORE
  END-IF
END-FOR
COMPUTE #CPU-ELAPSED = *CPU-TIME - #CPU-START
WRITE 3/10 'ARRAY TIME==>' *TIMD (0046) #CPU-ELAPSED
*
END
* 
* 
*     PAGE #   1                    DATE:    17-09-26
*     PROGRAM: EXAMIN09             LIBRARY: BOSTON01
*  
*  
*  
*          CONTROL LOOP TIME==>        2         24
*  
*  
*          STRING TIME==>       10        100
*  
*  
*          ARRAY TIME==>        8         78

